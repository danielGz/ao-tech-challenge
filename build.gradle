// Plugin definitions
plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'org.openapi.generator' version '7.7.0'
}

// Group and version settings
group = 'com.agileengine'
version = '0.0.1-SNAPSHOT'

// Java toolchain configuration
java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

// Dependency configurations
configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

// Repository configurations
repositories {
	mavenCentral()
}

// Dependency declarations
dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
	implementation 'javax.servlet:javax.servlet-api:4.0.1'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.20'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'org.postgresql:postgresql'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.h2database:h2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// OpenAPI Generator task configuration
openApiGenerate {
	inputSpec.set("$rootDir/src/main/resources/openapi.yaml")
	outputDir = "${rootDir}"
	println("OpenAPI outputDir : ${rootDir}")
	generatorName = "spring"
	apiPackage = "${group}.ecomm.openapi"
	invokerPackage = "${group}.ecomm.openapi"
	modelPackage = "${group}.ecomm.openapi.model"
	configOptions = [
			library: "spring-boot",
			dateLibrary: "java8",
			hideGenerationTimestamp: "true",
			serializableModel: "true",
			openApiNullable: "false",
			useTags: "true",
			hateoas: "true",
			unhandledException: "true",
			useSpringBoot3: "true",
			useSwaggerUI: "true",
			interfaceOnly: "true",
			skipDefaultInterface: "true"
	]
}

// Tasks for managing API generation and model processing
task generateJpaAnnotations(type: Copy) {
	dependsOn 'openApiGenerate'
	outputs.upToDateWhen { false }
	doFirst {
		println("Generating JPA annotations")
	}
	from "$rootDir/src/main/java/com/agileengine/ecomm/openapi/model"
	into "$buildDir/temp/model"
	include '**/*.java'

	Properties relations = new Properties()
	relations.load(new FileInputStream("$rootDir/src/main/resources/entity-relations.properties"))

	eachFile { FileCopyDetails details ->
		if (!details.file.text.contains("// JPA Annotations Processed")) {
			println("Adding annotations in ${details.file.name}")
			String content = details.file.text

			// Mark as processed
			content = "// JPA Annotations Processed\n" + content

			// Import JPA annotations if 'id' field exists
			if (content.contains("private Long id")) {
				content = content.replaceAll('package (.*);', '$0\nimport jakarta.persistence.*;\nimport com.fasterxml.jackson.annotation.*;\nimport com.fasterxml.jackson.annotation.JsonIdentityInfo;\nimport com.fasterxml.jackson.annotation.ObjectIdGenerators;')
				content = content.replaceAll('public class (.*) \\{', '@Entity\npublic class $1 {')
				content = content.replaceAll('private Long id;', '@Id\n  @GeneratedValue(strategy=GenerationType.AUTO)\n  private Long id;')
			}

			// Apply dynamic relations from properties
			relations.each { key, value ->
				String entity = key
				// Splitting on comma to handle multiple relationships
				value.split(',').each { relationship ->
					String relation = relationship.split(':')[0].trim()
					String relatedEntity = relationship.split(':')[1].trim()

					if (content.contains("class $entity")) {
						if (relation == "ManyToOne") {
							// Check for reciprocal OneToMany and add JsonIgnore
							if (relations.getProperty(relatedEntity)?.contains("OneToMany:" + entity)) {
								content = content.replaceAll('private ' + relatedEntity + ' (.*);', '@JsonIgnore\n  @ManyToOne\n  private ' + relatedEntity + ' $1;')
								String fieldName = relatedEntity[0].toLowerCase() + relatedEntity.substring(1)
								content = content.replaceAll('@JsonProperty\\("' + fieldName + '"\\)\\s*', '')
							}else{
								content = content.replaceAll('private ' + relatedEntity + ' (.*);', '@ManyToOne\n  private ' + relatedEntity + ' $1;')
							}
						} else if (relation == "OneToMany") {
							String mappedByField = entity[0].toLowerCase() + entity.substring(1)
							content = content.replaceAll('private List<.*' + relatedEntity + '> (.*);', '@OneToMany(mappedBy="' + mappedByField + '", cascade = CascadeType.ALL, fetch = FetchType.LAZY)\n  private List<' + relatedEntity + '> $1;')
						}
					}
				}
			}
			// Write the modified content back
			details.file.text = content
		}
	}
	doLast {
		copy {
			from "$buildDir/temp/model"
			into "$rootDir/src/main/java/com/agileengine/ecomm/openapi/model"
		}
		delete "$buildDir/temp/model"
		println("JPA annotations generation and copying complete.")
	}
}



task clearAndRegenerate {
	description = 'Clears previous API spec and regenerates JPA annotations'
	doFirst {
		def previousSpec = file("$rootDir/openapi-backup.yaml")
		if (previousSpec.exists()) {
			previousSpec.delete()
			println("Previous API spec file deleted.")
		} else {
			println("No previous API spec file found.")
		}
	}
	finalizedBy 'generateJpaAnnotations'
}

// Task dependencies and testing configuration
def tasksDependingOnOpenApi = ['jar', 'bootJar', 'resolveMainClassName', 'compileJava', 'processResources','processTestResources']
tasksDependingOnOpenApi.each { taskName ->
	tasks.named(taskName).configure {
		dependsOn 'generateJpaAnnotations'
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('openApiGenerate').configure {
	doFirst {
		def currentSpec = file("$rootDir/src/main/resources/openapi.yaml")
		def previousSpec = file("$rootDir/openapi-backup.yaml")
		if (!previousSpec.exists() || !currentSpec.text.equals(previousSpec.text)) {
			println("API spec has changed or previous spec missing. Proceeding with generation.")
		} else {
			println("No changes in API spec. Skipping OpenAPI generation.")
			throw new StopExecutionException("Skipping OpenAPI generation as no changes detected.")
		}
	}
	doLast {
		copy {
			from "$rootDir/src/main/resources/openapi.yaml"
			into "$rootDir"
			rename { "openapi-backup.yaml" }
		}
		println("OpenAPI generation completed. Spec has been updated.")
	}
}
